////////////////////////////////////////////////////////////////////////////////
//cusfreq.spl -Customisation Execution Frequency
//=========================================================================//
// Copyright (C) Velocity Global Ltd 2020
//
// PRONTO is a registered trademark of Pronto Software P/L.
//
// All Rights Reserved. Unauthorised copying is prohibited.
//=========================================================================//
//
// Modification History
// Date		Who	SDR		What
////////////////////////////////////////////////////////////////////////////////

// The SQL statement goes in here because we it occurs in three places and we
// want to be able to maintain it one location

#define NO_OF_SELECTION_PARAMETERS '1'
#define REPORT_NAME 'Customisation Execution Frequency'
//#define SQLSTMT \
//	select \
//		system-event-log.sel-code \
//		system-event-log.sel-date-time \
//		system-event-log.sel-login-id \
//		system-event-log.sel-seq \
//		system-event-log.sel-text \
//		system-event-log.sel-type \
//	from \
//		system-event-log \
//	WHERE system-event-log.sel-date-time > :ws-start-sel-date-time \
//	ORDER BY \
//		system-event-log.sel-type, \
//		system-event-log.sel-date-time \

#define SQLSTMT \
	select * from t-freqs


#include "../include/i8proglog.spl"
#include "../include/i8secabt.spl"
#include "../include/rbtchproc.spl"

link 'clib/clibvpdf'
#include "../include/i8ods.spl"
link 'clib/clibvods'
link 'clib/clibvfile'

field
	ws-function-title					pic x(80) type string
	ws-excel-flag						pic x
	ws-exec-start						type datetime
	ws-exec-end							type datetime
	ws-mail-recipients					pic x(1024) type string
	ws-spreadsheet-file-name			pic x(1024) type string
	ws-spool-id							pic x(20) type string
	ws-return							pic x(1024) type string // Return for app services
	ws-months-ago						pic 9(8)

object t-freqs
	type is memory
	record is 
		tf-pgm							pic x(30) type string
		tf-user							pic x(30) type string
		tf-month						type date
		tf-freq							type number
		tf-pgm-exists					type boolean
		tf-source-exists				type boolean
		tf-load-exists					type boolean
	end-record
	key is tf-pgm tf-user tf-month

object textfile
	type is external
	record
		txf-line							pic x(1024) type string
	end-record

object pgms
	type is memory
	record is 
		p-pgm-name							pic x(100) type string
		p-path								pic x(512) type string
		p-source							type boolean
		p-load								type boolean
	end-record
	key is p-pgm-name

procedure main
returning
	lr-return							pic x(1024) type string
local
	lf-para-no							type number
	get system-control first
	get system-user-control	
		on index user-id											
		key is login-id()
	on error														
	endon															
	do i8secabt-check-abort parameters
		sys-consolidation-division login-id() "ZVG" "R017"
			returning ws-function-title
	set ws-report-started = FALSE
	do i8proglog-write-log
	// if the program is on the batch queue then the first if statement will
	// handle the parameters
	if get-param(1) = '-b' and get-param(3) = '-sequence'
		RBTCHPROC_RUN_PROGRAM(REPORT_NAME)
	elseif param-count() > 0
		// If parameters were passed AND it is not on the batch queue, then
		// the parameters will be the selection parameters followed by the
		// default parameters
		// Note that ALL parameters MUST be specified
		set lf-para-no = 1
		set ws-months-ago = num(get-param(lf-para-no))
		set lf-para-no += 1
		// remaining  - use offsets from total no of parameters
		set ws-mail-recipients = get-param(lf-para-no + 1)
		set ws-excel-flag = get-param(lf-para-no + 2)
		//
		if ws-excel-flag not in (NO, "D", "S")
			set ws-excel-flag = NO
		endif
		set ws-spreadsheet-file-name = get-param(lf-para-no + 3)
		do rbtchproc-report-detail
		set lr-return = "Program Completed Successfully"
	else
		RBTCHPROC_RUN_PROGRAM(REPORT_NAME)
	endif
	if ws-return =  spaces
		set lr-return =  "Program completed successfully"
	else
		set lr-return  = ws-return
	endif
endprocedure //main ------------------------------------------------------------

#if BMS_DICT_VER > 7600

screen rbtchproc-report-parameters
local
	i 						type number
	allow entry
	window
		title ws-function-title
	responsive
before
	set ws-months-ago = 24
detail
	screen-group // ---- Parameters ----------------------------
		title "Parameters"
	accept ws-months-ago
		// add remove following lines as necessary
		title 'Number of Months ago to look at :'
		optional
		help "This should be a positive number.  ie. 24 is 24 months prior to today"
		default ws-months-ago
	before-accept
	end-before-accept
	end-screen-group // ---- Parameters --------------------------------------
	screen-group // ---- Output Options ----------------------------
		title "Output Options"
		field-group // ---- Export ----------------------------
			title "Export"
			radio-button ws-excel-flag
				NO title "No Export"
				"D" title "View Datagrid"
				"S" title "Create Spreadsheet file"
				default "NO"
				help "N)o export D)isplay data as a datagrid"
			on change
				refresh ws-mail-recipients
			endon
		end-field-group // ---- Export --------------------------------------
		accept ws-mail-recipients
			title "Email:"
			help "Enter space, comma or semi-colon separated list of email recipients"
			when ws-excel-flag in (NO "S")
			no-warning
			optional
		accept ws-spreadsheet-file-name
			title "SpreadSheet File"
			// file will be emailed if email address specified
			when ws-excel-flag in ( "S" ) and ws-mail-recipients = spaces
			help "Enter name for the file.  Default location will be data directory"
			default ws-spreadsheet-file-name
			no-warning
			before-accept
				if ws-spreadsheet-file-name = spaces
					set ws-spreadsheet-file-name = str-concat("/tmp/" REPORT_NAME ".ods")
					for i = 1 to str-len(ws-spreadsheet-file-name)
						if substring(ws-spreadsheet-file-name,i,i) = space
							string ws-spreadsheet-file-name replacing "_" at i
						endif
					endfor
				endif
			end-before-accept
	end-screen-group // ---- Output Options --------------------------------------
	confirm auto
	confirmed
		if ws-excel-flag = "D"
			do build-t-freqs
			do display-datagrid
		else
			RBTCHPROC_EXECUTION
			exit 0
		endif
	endconfirm
endscreen //rbtchproc-report-parameters --------------------------------------

#else

screen rbtchproc-report-parameters
local
	i 						type number
	allow entry
	window
		title ws-function-title
	form-entry
before
	set ws-months-ago = min-value(ws-months-ago)
	box @1,1 to @19,80
		title "Parameters"
	box @20,1 to @24,80
		title "Output Options"
detail
	accept ws-months-ago
		// add remove following lines as necessary
		title 'Number of Months ago to look at :'
		optional
		help "This should be a positive number.  ie. 24 is 24 months prior to today"
		default ws-months-ago
	before-accept
	end-before-accept
	radio-button ws-excel-flag
		@21,10 NO title "No Export"
		@21,20 "D" title "View Datagrid"
		@21,33 "S" title "Create Spreadsheet file"
		default "NO"
		help "N)o export D)isplay data as a datagrid"
	on change
		refresh ws-mail-recipients
	endon
	accept ws-mail-recipients @22,15 pic x(60)
		title "Email:"
		help "Enter space, comma or semi-colon separated list of email recipients"
		when ws-excel-flag in (NO "S")
		no-warning
		optional
	accept ws-spreadsheet-file-name @23,15 pic x(60)
		title "SpreadSheet File:"
		// file will be emailed if email address specified
		when ws-excel-flag in ( "S" ) and ws-mail-recipients = spaces
		help "Enter name for the file.  Default location will be data directory"
		default ws-spreadsheet-file-name
		no-warning
		before-accept
			if ws-spreadsheet-file-name = spaces
				set ws-spreadsheet-file-name = str-concat("/tmp/" REPORT_NAME ".ods")
				for i = 1 to str-len(ws-spreadsheet-file-name)
					if substring(ws-spreadsheet-file-name,i,i) = space
						string ws-spreadsheet-file-name replacing "_" at i
					endif
				endfor
			endif
		end-before-accept
	confirm auto
	confirmed
		if ws-excel-flag = "D"
			do build-t-freqs
			do display-datagrid
		else
			RBTCHPROC_EXECUTION
			exit 0
		endif
	endconfirm
endscreen //rbtchproc-report-parameters --------------------------------------

#endif

procedure rbtchproc-set-parameters
	RBTCHPROC_LOAD_SET_NUM(ws-months-ago)
	RBTCHPROC_LOAD_SET_ALPHA(ws-mail-recipients)
	RBTCHPROC_LOAD_SET_ALPHA(ws-spreadsheet-file-name)
	RBTCHPROC_LOAD_SET_ALPHA(ws-excel-flag)
endprocedure //rbtchproc-set-parameters --------------------------------------

procedure start-report
	set rbtchproc-report-name = REPORT_NAME
	report rbtchproc-report-name		
		header is skeleton-report-header
		no-message
		width 132
		full-xml
	set ws-report-started = TRUE
	if rbtchproc-in-background
		RBTCHPROC_MESSAGE_REPORT_STARTED
	else
		display 'Report :' @20,59 background right bold
		display spool-file-name() @20,68 pic x(12) foreground prompts
		display "Accessing Database" @21,59 background left bold
	endif
	set ws-exec-start = sys-time()
	set ws-spool-id = spool-file-name()
endprocedure //start-report ----------------------------------------------------

procedure skeleton-report-header
	if report-is-xml()
		report section "headings_section"
	endif
	print company-name col 1
		rbtchproc-report-name col 51 bold
		"Date:" col 85
		tod() col 110
		today() col 91
		"Time:" col 104
		'Page:' col 120
		left-justify(str(page-no)) col 126 pic x(4)
	skip
	print
		"Type" in col    1
		right-justify("Date",8) in col    7 pic x(8) right
		"User" in col   17
		right-justify("Seq.",2) in col   49 pic x(2) right
		"Event" in col   55
		"Text" in col   82
	print
		right-justify("/",8) in col    7 pic x(8) right
		"Name" in col   17
	print
		right-justify("Time",8) in col    7 pic x(8) right
	if report-is-xml()
		report section finished
	endif
endprocedure //skeleton-report-header ------------------------------------------

// Your main processing routine must be named as follows
procedure rbtchproc-report-detail
local
	lf-rec-counter				type number
	window @19,44 to @24,80
	    title is "Report Progress"
	    color white
	    no-hide
	if not rbtchproc-in-background
		display bitmap concat(getenv('BMS')'/images/repedge.gif') @19,44
	endif
	//
	// PIC clauses are set in the print statement to enable
	// later use of the pronto report layout tool
	//
	do build-t-freqs
	if ws-excel-flag = "S"
		do create-spreadsheet
		exit 0
	endif
	do start-report
	page
	//  The sql statement is a # define because it used in three places
	SQLSTMT
	detail
		report section 'detail_line'
		print
			tf-pgm	  in col   1 pic x(20)
				tag is 'tf_pgm'
			tf-user  in col   25 pic x(20)
				tag is 'tf_user'
			tf-month  in col  50 
				tag is 'tf_month'
			tf-freq  in col  65 pic z(5)
				tag is 'tf_freq'
		report section finished
		set lf-rec-counter += 1
		if not rbtchproc-in-background
		and time-elapsed(1000,1)
			display "Printing Report" @21,59 background left bold
			display "Now at :" @23,45 background prompts left
			display sel-type @23,54 foreground prompts
		endif
	end-select
	report section 'grand_total'
	print 'GRAND TOTAL' nonewline
		tag 'grand_total_prompt'
	report section finished
	do finish-report
	set ws-return = concat(str(lf-rec-counter) " Records Read")
endprocedure //rbtchproc-report-detail ---------------------------------------


procedure finish-report
	set ws-exec-end = sys-time()
	if report-is-xml()
		page
		report section "options_page"
	endif
	if ws-report-started
		skip 2
		print 'Options selected : ' col 2 no-newline
			tag  'options_print'
		print 'Earliest Date       >  : ' in col 22
			 tag is 'prompt_ws_start_sel_date_time'
			ws-months-ago         tag is 'ws_months_ago'
			tag  'options_date'
		print 'Execution Time Start           = : ' in col 22
			 tag is 'prompt_ws_start_time'
			ws-exec-start                 tag is 'ws_start_time'
		print 'Execution Time End             = : ' in col 22
			 tag is 'prompt_ws_end_time'
			ws-exec-end                   tag is 'ws_end_time'
		skip
	endif
	if report-is-xml()
		report section finished
	endif
	RBTCHPROC_SCREEN_FINISH_MESSAGE(ws-report-started,23,45)
	if ws-mail-recipients <> spaces
		do mail-spool-file
	endif
endprocedure //finish-report ---------------------------------------------------

screen display-datagrid
	window @1,1 to @24,80
	title REPORT_NAME
	datagrid occurs 22
	//  The sql statement is a # define because it used in three places
	SQLSTMT
	allowed search
detail
	display tf-pgm @1,1
		Title "Type  "
	display tf-user  @1,2
		Title "User"
	display tf-month  @1,3
		Title "Month "
	display tf-freq  @1,4
		Title "Frequency "
	display if-then-else(tf-pgm-exists,YES,NO) @1,5
		title "Pgm Found"
	display if-then-else(tf-source-exists,YES,NO) @1,6
		title "Source Found"
	display if-then-else(tf-load-exists,YES,NO) @1,7
		title "Load Found"
end-screen

procedure mail-spool-file
local
	lf-address							pic x(256) type string
	lf-pdf-filename						pic x(256) type string
	i 									type number
	lf-pdf-failed						type boolean
	set ws-return = concat("Report mailed to " ws-mail-recipients)
	report finished
	//
	// convert file to pdf
	//
	set lf-pdf-filename = concat("/tmp/" REPORT_NAME ".pdf")
	for i = 1 to str-len(lf-pdf-filename)
		if substring(lf-pdf-filename,i,i) = " "
			string lf-pdf-filename replacing "_" at i
		endif
	endfor
	do clibvpdf-create parameters are ws-spool-id  lf-pdf-filename
	if exit-status <= 0
		set lf-pdf-failed = FALSE
	else
		set lf-pdf-failed = TRUE
	endif
	//
	if mail-start(REPORT_NAME) endif
	if mail-add-line("Here is the content of the report") endif
	if lf-pdf-failed
#if BMS_DICT_VER < 7600
		if mail-attach(ws-spool-id,str-concat(REPORT_NAME,".html"),0,1,1) endif
#else
		if mail-attach(ws-spool-id,spaces,0,1,1) endif
#endif
	else
		if mail-attach(lf-pdf-filename,spaces,0,1,1) endif
	endif
	while ws-mail-recipients <> spaces
		if lf-address <> spaces and substring(ws-mail-recipients,1,1) in { space "," ";" }
			// someone to email
			if mail-send(lf-address,FALSE,FALSE) endif
			set lf-address = spaces
		else
			string lf-address appending substring(ws-mail-recipients,1,1)
		endif
		string ws-mail-recipients deleting 1
	end-while
	// at the end there may be one more address left over
	if lf-address <> spaces
		// someone to email
		if mail-send(lf-address,FALSE,FALSE) endif
	endif
	if mail-cancel() endif
end-procedure

procedure create-spreadsheet
local
	lf-row								type number
	lf-address							pic x(256) type string
	lf-data-start-for-totals			type number
//
//	Define the styles to be used.   Provided here.  Generator uses lf- values
//	so that only the skeleton needs changing to make a change to the output.
//
	lf-header-style					type string
	lf-total-style						type string
	lf-detail-style						type string
	//
	//
	do clibvods-start-export parameters "/tmp"
	do clibvods-add-sheet parameters REPORT_NAME True
	do create-spreadsheet-styles
	//
	// Change here to use a different style
	//
	set lf-header-style = I8ODS_CELL_STYLE_HEADER
	set lf-detail-style = I8ODS_CELL_STYLE_DETAIL
	set lf-total-style = I8ODS_CELL_STYLE_TOTAL_NUMERIC
	// This is the best guess at column widths.  May need reviewing
	do clibvods-add-sheet-column parameters '8cm' 1 // tf-pgm
	do clibvods-add-sheet-column parameters '8cm' 1 // user
	do clibvods-add-sheet-column parameters spaces 1 // date
	do clibvods-add-sheet-column parameters spaces 1 // freq
	do clibvods-add-sheet-column parameters spaces 1 // Exusts
	do clibvods-add-sheet-column parameters spaces 1 // Source Exists
	do clibvods-add-sheet-column parameters spaces 1 // Load Exists
	//
	do clibvods-add-sheet-row
	set lf-row += 1
	do clibvods-fill-a-cell-alpha-span parameters "MAINHEAD" REPORT_NAME 7
	do clibvods-add-sheet-row
	set lf-row += 1
	do clibvods-add-sheet-row
	set lf-row += 1
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'Pgm'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'User'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'Month'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'Exec Freq'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'Pgm Exists'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'Source Exists'
	do clibvods-fill-a-cell-alpha parameters lf-header-style 'load Exists'
//	SQL
	set lf-data-start-for-totals = lf-row + 1 // This enables changing in the skeleton rather than changing the generator.
	//  The sql statement is a # define because it used in three places
	SQLSTMT
	detail
		do clibvods-add-sheet-row
		set lf-row += 1
		do clibvods-fill-a-cell-alpha parameters lf-detail-style tf-pgm
		do clibvods-fill-a-cell-alpha parameters lf-detail-style tf-user
		do clibvods-fill-a-cell-date parameters lf-detail-style tf-month
		do clibvods-fill-a-cell-numeric parameters lf-detail-style tf-freq
		do clibvods-fill-a-cell-alpha parameters lf-detail-style if-then-else(tf-pgm-exists,YES,NO)
		do clibvods-fill-a-cell-alpha parameters lf-detail-style if-then-else(tf-source-exists,YES,NO)
		do clibvods-fill-a-cell-alpha parameters lf-detail-style if-then-else(tf-load-exists,YES,NO)
	end-select
	if ws-spreadsheet-file-name = spaces
		set ws-spreadsheet-file-name = str-concat("/tmp/report_spreadsheet_" str(pid) ".ods")
		set ws-spreadsheet-file-name = str-concat("/tmp/" REPORT_NAME ".ods")
	endif
	do clibvods-finish-export parameters are ws-spreadsheet-file-name
	if ws-mail-recipients != spaces
		set ws-return = concat("Spreadsheet mailed to " ws-mail-recipients)
		if mail-start(REPORT_NAME) endif
		if mail-add-line("Spreadsheet as attached") endif
		if mail-attach(ws-spreadsheet-file-name, spaces,0,0,1) endif
		while ws-mail-recipients <> spaces
			if lf-address <> spaces and substring(ws-mail-recipients,1,1) in { space "," ";" }
				// someone to email
				if mail-send(lf-address,FALSE,FALSE) endif
				set lf-address = spaces
			else
				string lf-address appending substring(ws-mail-recipients,1,1)
			endif
			string ws-mail-recipients deleting 1
		end-while
		// at the end there may be one more address left over
		if lf-address <> spaces
			// someone to email
			if mail-send(lf-address,FALSE,FALSE) endif
		endif
		if mail-cancel() endif	
	elseif not batched()
		do clibvods-transfer-file-to-pc-web-client parameters are ws-spreadsheet-file-name
	else
		set ws-return = concat("Spreadsheet Created: " ws-spreadsheet-file-name)
	endif
end-procedure

procedure create-spreadsheet-styles
	do clibvods-create-column-style parameters "8cm" "8cm"
	do clibvods-create-cell-style parameters
		"MAINHEAD" //cell-name				
			"thick" //border-thickness			
			"#DD5044" //border-colour			
			"#72F0FE" //background-color			
			"#305496"//text-colour				
			"center" //text-align
			"Bold" //font-weight				
			"Letter Gothic" //font-name				
			"20pt" //font-size				
			space //data-style	
			TRUE //Top Border	
			TRUE //Bottom Border
			TRUE //Left Border
			TRUE //Right Border
			FALSE // Wrap text
			"middle" // vertical align
			FALSE // italic
			FALSE // underline
	//
	// The following two are not used in the generated program but
	// are provided to enable further customisation that might require
	// 2dp currency numbers
	//
	// Your program should simply use the "mycurrency" style when
	// writing a cell.  You chould not need to access the
	// "curr2dp".
	//
	do clibvods-create-currency-style parameters
		"curr2dp"
		spaces
		spaces
		spaces
		True
		2
		TRUE // red when below zero
	do clibvods-create-cell-style parameters
		"mycurrency" //cell-name				
			"thin" //border-thickness			
			I8ODS_DEFAULT_DETAIL_BORDER_COLOUR //border-colour			
			I8ODS_DEFAULT_DETAIL_BACK_COLOUR //background-color			
			I8ODS_DEFAULT_DETAIL_TEXT_COLOUR //text-colour				
			spaces //text-align
			spaces //font-weight				
			spaces //font-name				
			spaces //font-size				
			"curr2dp" //data-style	
			TRUE //Top Border	
			TRUE //Bottom Border
			TRUE //Left Border
			TRUE //Right Border
			FALSE // Wrap text
			spaces // vertical align
			FALSE // italic
			FALSE // underline
end-procedure

procedure build-t-freqs
local 
	lf-sql-datetime								type datetime
	//
	close t-freqs and remove
	open t-freqs temporary
	on error
		exit 1
	endon
	do build-cus
	//
	set lf-sql-datetime = date-time(addmonths(julian(1,month(today()),year(today)),0 - ws-months-ago,TRUE),0,TRUE)
	select * from system-event-log
		where sel-type = 'CLOG'
		and sel-date-time >= :lf-sql-datetime
	detail
		initialise t-freqs
		set tf-pgm = sub-string(sel-login-id,1,pattern(sel-login-id," "))
		set tf-user = sub-string(sel-login-id,pattern(sel-login-id," ") + 1,str-len(sel-login-id))
		set tf-month = date-from-date-time(sel-date-time,true)
		set tf-freq = num(sel-code)
		get pgms
			on index p-pgm-name
			key is tf-pgm
		on error ENOREC
			set tf-pgm-exists = FALSE
		else
			set tf-source-exists = p-source
			set tf-load-exists = p-load
		endon
		insert t-freqs
	end-select
	// add in any pgms that exist but are not in the list so that we know
	// we have code that has NEVER been run...
	select * from pgms
	detail
		get t-freqs 
			on index tf-pgm
			key is p-pgm-name
		on error ENOREC
			initialise t-freqs
			set tf-pgm = p-pgm-name
			insert t-freqs
		endon
	end-select
end-procedure


procedure build-cus
local
	lf-file-name							pic x(100) type string
	lf-cmd									type string pic x(1024)
	lf-file									pic x(256) type string
	lf-path									pic x(256) type string
	lf-ext									pic x(20) type string
	lf-drive								pic x(20) type string
	//
	set lf-file-name = str-concat("/tmp/ls", str(pid()), ".csv")
	set lf-cmd = concat('find $CUS \( -name "*.spl" -o -name "*.op?" \) > ' lf-file-name)
	command "sh" parameters are "-c" lf-cmd
	close pgms and remove
	open pgms temporary
	open textfile file is lf-file-name
	on error 
		exit 1
	endon
	select * from textfile
	detail
		do clibvfile-get-parts parameters are txf-line
			returning lf-drive lf-path lf-file lf-ext
		get pgms
			on index p-pgm-name
			key is lf-file
		on error ENOREC
			initialise pgms
			set p-pgm-name = lf-file
			set p-path = lf-path
			if pattern(lf-ext,"spl") > 0
				set p-source = TRUE
			endif
			if pattern(lf-ext,"op") > 0
				set p-load = TRUE
			endif
			insert pgms
		else
			if pattern(lf-ext,"spl") > 0
				set p-source = TRUE
			endif
			if pattern(lf-ext,"op") > 0
				set p-load = TRUE
			endif
			update pgms
		endon
	end-select
end-procedure

