////////////////////////////////////////////////////////////////////////////////
// Program : screen overrides for m54custom
//=========================================================================//
// Copyright (C) Velocity Global Ltd 2018
//
// PRONTO is a registered trademark of PRONTO Software P/L.
//
// All Rights Reserved. Unauthorized copying is prohibited.
//=========================================================================//
//
// File: screens/pcsm54custom.spl
//
// Modification History
// Date		Who	SDR		What
// 01Jun18  rjb         written 
////////////////////////////////////////////////////////////////////////////////
 
#include "../include/i8secabt.spl"
#include "../include/i5sostatii.spl"
#include "../include/i8proglogparam.spl"
 
mode md-select
	prompt "Select"
mode md-type
	prompt "Type"
	help "Select by Type"

procedure library-init
	get system-control first
	on error
		abort "No Control Record"
	endon
	do i8proglogparam-write-log parameters are "pcsm54custom"
end-procedure

procedure get-sales-order user-trigger
parameters
	lp-flag								pic x(20)
returning
	lr-so-order-no						like so-order-no
local
	lf-so-bo-suffix						like so-bo-suffix
	do sel-sales-order
		parameters are lp-flag
		returning lr-so-order-no lf-so-bo-suffix
end-procedure

screen sel-sales-order 
window
parameters
	lp-flag									pic x(20)
returning
	lr-so-order-no							like so-order-no
	lr-so-bo-suffix							like so-bo-suffix
local
	lf-all-selected							type boolean
	//
	select t0.so-order-no
		t0.so-bo-suffix
		t0.so-cust-code
		t0.so-order-status
		t0.so-order-date
		t0.so-delivery-date
		t0.so-whse-code
		t0.so-territory-code
		t0.so-rep-code
		t1.sosd-description
		t2.shortname
	from sales-order as t0
		left outer join sales-order-status-desc as t1 on t1.sosd-language = spaces and t1.sosd-status = t0.so-order-status
		left outer join deb-master as t2 on t0.so-cust-code = t2.accountcode
	where t0.so-order-status < '90'
	and ( 
		( :lf-all-selected = TRUE )
		or ( :lp-flag in ('-confirm') and t0.so-order-status in {
				I5SO_ENTERED
				I5SO_BACK_ORDER
				I5SO_CONSIGNMENT_ENTERED
				I5SO_CNOTE_ENTERED
				I5SO_QUOTE_PRINTED
				I5SO_TRANSFER_ENTERED
				I5SO_ENTERED_TO_BE_MANUFACTURED
				I5SO_ENTERED_CREDIT_HOLD
				I5SO_FORWARD_ORDER
				I5SO_TO_BE_MANUF_AND_PURCHASED
				I5SO_ENTERED_ON_HOLD
				I5SO_CONSIGNMENT_BACKORDER
				I5SO_CONTRACT_ORDER
				I5SO_ENTERED_CNOTE_ON_HOLD
				I5SO_REQUISITION_ON_HOLD
				I5SO_TRANSFER_ON_BACKORDER
				I5SO_TRANSFER_TO_BE_MANUFACTURED
				I5SO_ASSEMBLY_ON_HOLD
				I5SO_FORWARD_TRANSFER
				I5SO_RMA_APPROVED_FOR_PRINTING
			})
		or (:lp-flag = '-pick' and t0.so-order-status in { 
				I5SO_RDY_FOR_PICKING				
				I5SO_BACK_ORDER_RDY_FOR_PICKING	
				I5SO_CONSIGNMENT_RDY_TO_PICK
				I5SO_CNOTE_RDY_TO_PICK	
				I5SO_RDY_TO_BE_PURCHASED
				I5SO_RDY_TO_PICK_TRANSFER
				I5SO_RDY_TO_RAISE_WORK_ORDER
				I5SO_RDY_TO_PICK_ASSEMBLY
				I5SO_TRANSFER_TO_BE_PURCHASED
				I5SO_RMA_IN_PROGRESS		
			})
		or (:lp-flag = '-docket' and t0.so-order-status in {
				I5SO_PICKING_PRINTED
				I5SO_CONSIGNMENT_PICKING_PRINTED
				I5SO_CNOTE_PICKING_PRINTED
				I5SO_TRANSFER_PICKING_PRINTED
				I5SO_ASSEMBLY_PICKING_PRINTED
				I5SO_RDY_TO_ASSEMBLE
				I5SO_RMA_REQUEST_FOR_CREDIT_PRTD
				I5SO_RMA_APPROVED
				I5SO_TRANSFER_ASSEMBLY_IN_PROGRESS
		})
		or (:lp-flag = spaces and t0.so-order-status in {
				I5SO_PICKING_PRINTED
				I5SO_CONSIGNMENT_PICKING_PRINTED
				I5SO_CNOTE_PICKING_PRINTED
				I5SO_TRANSFER_PICKING_PRINTED
				I5SO_ASSEMBLY_PICKING_PRINTED
				I5SO_RDY_TO_ASSEMBLE
				I5SO_RMA_REQUEST_FOR_CREDIT_PRTD
				I5SO_RMA_APPROVED
				I5SO_TRANSFER_ASSEMBLY_IN_PROGRESS
				I5SO_RDY_TO_PRINT_INVOICE
				I5SO_CONSIGNMENT_RDY_TO_PRINT
				I5SO_CNOTE_RDY_TO_PRINT
				I5SO_TRANSFER_IN_PROGRESS
		})
		)
	datagrid occurs 22
	allowed md-select search md-type
before 
	set lf-all-selected = TRUE
detail
	if screenmode = md-select
		set lr-so-order-no = so-order-no 
		set lr-so-bo-suffix = so-bo-suffix
		exit
	endif
	if screenmode = md-type
		if lf-all-selected
			do type-selector entry once
				returning lp-flag
			set lf-all-selected = FALSE
		else
			set lf-all-selected = TRUE
		endif
		reselect
		refresh review
	endif
	accept so-order-no @1,1
		title "Order No"
		when screenmode = search
		show-value
	accept so-bo-suffix @1,2 pic x(4)
		title "Sfx"
		when screenmode = search
		show-value
	display so-order-status @1,3
		title "Code"
	display sosd-description @1,4 pic x(20)
		title "Status"
	display so-order-date @1,5
		title "Date"
	display so-cust-code @1,6 pic x(20)
		title "Customer"
	display shortname @1,7
		title "Name"
	display so-delivery-date @1,8
		title "Delivery"
	display so-whse-code @1,9
		title "Whse"
	display so-territory-code @1,10
		title "Terr"
	display so-rep-code @1,11
		title "Rep"
end-screen

screen type-selector
returning 
	lr-type								pic x(20)
window @1,1 to @5,20
	window-position 5
	form-entry
	no-ok-cancel
detail
	radio-button lr-type
		@1,2 "-pick" title  "Picking Slips"
		@2,2 "-confirm" title "Confirmations"
		@3,2 spaces title "Invoices / Cr Notes"
//		right-coordinate
		default spaces
	end-radio-button 
end-screen
